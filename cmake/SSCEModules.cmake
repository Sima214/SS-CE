function(declare_module modname doc)
    ## Create docstring for dependencies.
    set( deps "" )
    if(NOT "${ARGN}" STREQUAL "")
        set( deps "\nDepends on: " )
    endif()
    foreach(d ${ARGN})
        set( deps "${deps}${d}, " )
    endforeach(d)
    ## Remove trailing comma and space.
    if(NOT "${ARGN}" STREQUAL "")
        string( LENGTH ${deps} deps_len )
        string( SUBSTRING ${deps} 0 ${deps_len}-2 ${deps} )
    endif()
    ## Load variable from the cache.
    set( ${modname} TRUE CACHE BOOL "${doc}${deps}" )
    ## Append module definition.
    if(${${modname}})
        set( MODULE_DEFINITIONS "${MODULE_DEFINITIONS}#define ${modname}\n" PARENT_SCOPE )
    endif(${${modname}})
    ## Check dependencies.
    if(${${modname}})
        foreach(d ${ARGN})
            if(NOT ${${d}})
                message( SEND_ERROR "${modname} requires ${d}, but ${d} is disabled!" )
            endif(NOT ${${d}})
        endforeach(d)
    endif(${${modname}})
endfunction()

function(define_module modname src inc)
    if(${${modname}})
        message( STATUS "${modname}:\tON" )
        string( REGEX REPLACE "MODULE_" "" namestripped ${modname} )
        string( TOLOWER ${namestripped} mnr )
        string( REGEX REPLACE "_" "/" mn ${mnr} )
        set( folder "${SSCE_DIR_SRC}/${mn}" )
        set( SSCE_INC_DIRS ${SSCE_INC_DIRS} ${folder} PARENT_SCOPE )
        include_directories( ${folder} )
        foreach(cc ${src})
            list( APPEND srclist "${folder}/${cc}" )
        endforeach(cc ${src})
        set( SSCE_SRC ${SSCE_SRC} ${srclist} PARENT_SCOPE )
        foreach(ch ${inc})
            list( APPEND headerlist "${folder}/${ch}" )
        endforeach(ch ${inc})
        set( SSCE_INC ${SSCE_INC} ${headerlist} PARENT_SCOPE )
    else(${${modname}})
        message( STATUS "${modname}:\tOFF" )
    endif(${${modname}})
endfunction()

function(define_test modname)
    if(${${modname}})
        string( REGEX REPLACE "MODULE_" "" name "${modname}" )
        string( TOLOWER "${name}" name )
        message( STATUS "Adding tests for ${name}:" )
        foreach(d ${ARGN})
            set( subname "${name}_${d}" )
            if( EXISTS "${SSCE_DIR_TEST}/${subname}.c" )
                message( STATUS "\tAdding ${d} test." )
                add_executable( "${PROJECT_NAME}_${subname}" "${SSCE_DIR_TEST}/${subname}.c" )
                target_compile_options( "${PROJECT_NAME}_${subname}" PRIVATE "-fno-builtin" )
                target_link_libraries( "${PROJECT_NAME}_${subname}" ${PROJECT_NAME} )
                install( TARGETS "${PROJECT_NAME}_${subname}" RUNTIME DESTINATION "bin" )
                add_test( NAME ${subname} COMMAND "${PROJECT_NAME}_${subname}" )
            endif()
            if( EXISTS "${SSCE_DIR_TEST}/${subname}.cpp" )
                message( STATUS "\tAdding ${d}++ test." )
                add_executable( "${PROJECT_NAME}_${subname}_cpp" "${SSCE_DIR_TEST}/${subname}.cpp" )
                target_compile_options( "${PROJECT_NAME}_${subname}_cpp" PRIVATE "-fno-builtin" )
                target_link_libraries( "${PROJECT_NAME}_${subname}_cpp" ${PROJECT_NAME} )
                install( TARGETS "${PROJECT_NAME}_${subname}_cpp" RUNTIME DESTINATION "bin" )
                add_test( NAME "${subname}++" COMMAND "${PROJECT_NAME}_${subname}_cpp" )
            endif()
        endforeach(d)
    endif(${${modname}})
endfunction()